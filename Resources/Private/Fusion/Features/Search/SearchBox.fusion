prototype(Shel.Neos.Logs:Component.SearchBox) < prototype(Neos.Fusion:Component) {
    @propTypes {
        exceptions = ${PropTypes.arrayOf(PropTypes.instanceOf('Shel\Neos\Logs\Domain\ParsedException'))}
    }

    exceptions = ${[]}

    @private {
        exceptionDetailsUriTemplate = Neos.Fusion:UriBuilder {
            action = "showException"
            arguments {
                identifier = '__IDENTIFIER__'
            }
        }
    }

    renderer = afx`
        <div class="search-box">
            <script type="application/json" id="exceptions-data">{Json.stringify(props.exceptions)}</script>
            <input
                id="logs-search-box"
                type="search"
                disabled
                placeholder="Search in exceptions"
                data-exception-details-uri-template={private.exceptionDetailsUriTemplate}
            />
            <ol id="logs-search-results">
            </ol>
        </div>
    `
}
