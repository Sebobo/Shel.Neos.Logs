prototype(Shel.Neos.Logs:Component.ExceptionList.Item) < prototype(Neos.Fusion:Component) {
    @propTypes {
        exception = ${PropTypes.instanceOf('Shel\Neos\Logs\Model\ParsedException')}
    }
    exception = null

    renderer = afx`
        <tr style={'line-height: 1.3'}>
            <td>
                {exception.recent ? '<i class="fas fa-exclamation-circle" title="Recent"></i>' : ''}
            </td>
            <td>
                {Date.formatCldr(exception.date, 'yyyy / MM / dd - HH:mm')}
            </td>
            <td class="column__name">
                <a title={'View ' + exception.identifier}>
                    <Neos.Fusion:UriBuilder
                        @path="attributes.href"
                        action="showException"
                        arguments={{'identifier': exception.identifier}}
                    />
                    {exception.identifier}
                </a>
            </td>
            <td class="column__excerpt">
                <div>
                    <span title={exception.excerpt}>{exception.excerpt}</span>
                </div>
            </td>
            <td class="column__actions">
                <a title={'Download exception ' + exception.identifier}>
                    <Neos.Fusion:UriBuilder
                        @path="attributes.href"
                        action="downloadException"
                        arguments={{'identifier': exception.identifier}}
                    />
                    <i class="fas fa-download"></i>
                </a>
                <a title={'Delete exception ' + exception.identifier}>
                    <Neos.Fusion:UriBuilder
                        @path="attributes.href"
                        action="deleteException"
                        arguments={{'identifier': exception.identifier}}
                    />
                    <i class="fas fa-trash"></i>
                </a>
            </td>
        </tr>
    `
}
