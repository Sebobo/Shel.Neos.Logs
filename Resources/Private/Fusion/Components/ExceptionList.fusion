prototype(Shel.Neos.Logs:Component.ExceptionList) < prototype(Neos.Fusion:Component) {
    exceptions = ${[]}

    renderer = afx`
        <table class="neos-table">
            <thead>
                <tr>
                    <td>Date</td>
                    <td>Name</td>
                    <td>Excerpt</td>
                    <td>Actions</td>
                </tr>
            </thead>
            <tbody>
            <Neos.Fusion:Loop items={props.exceptions} itemName="file">
                <tr style={'line-height: 1.3'}>
                    <td>
                        {Date.formatCldr(file.date, 'yyyy / MM / dd - HH:mm')}
                    </td>
                    <td class="column__name">
                        <a title={'View ' + file.identifier}>
                            <Neos.Fusion:UriBuilder
                                @path="attributes.href"
                                action="showException"
                                arguments={{'filename': file.identifier}}
                            />
                            {file.identifier}
                        </a>
                    </td>
                    <td class="column__excerpt">
                        <div>
                            <span title={file.excerpt}>{file.excerpt}</span>
                        </div>
                    </td>
                    <td class="column__actions">
                        <a title={'Download exception ' + file.identifier}>
                            <Neos.Fusion:UriBuilder
                                @path="attributes.href"
                                action="downloadException"
                                arguments={{'filename': file.identifier}}
                            />
                            <i class="fas fa-download"></i>
                        </a>
                        <a title={'Delete exception ' + file.identifier}>
                            <Neos.Fusion:UriBuilder
                                @path="attributes.href"
                                action="deleteException"
                                arguments={{'filename': file.identifier}}
                            />
                            <i class="fas fa-trash"></i>
                        </a>
                    </td>
                </tr>
            </Neos.Fusion:Loop>
            </tbody>
        </table>
    `
}
